plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'me.fallenbreath.yamlang' version '1.4.1'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	def strictMaven = { String link, String... groups ->
		exclusiveContent {
			forRepository {
				maven { url = link }
			}
			filter {
				groups.each { includeGroup(it) }
			}
		}
	}

	strictMaven("https://api.modrinth.com/maven", "maven.modrinth")
	strictMaven("https://www.cursemaven.com", "curse.maven")

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modLocalRuntime modCompileOnly("maven.modrinth:lithium:${project.lithium}")
	modImplementation "curse.maven:carpet-349239:${project.carpet}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

yamlang {
	targetSourceSets = [sourceSets.main]
	inputDir = 'assets/shulkerfix/lang'
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}